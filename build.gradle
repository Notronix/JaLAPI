apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
group = 'com.notronix'
version = System.getenv("BUILD_NUMBER") ?: 'SNAPSHOT-' + getDate()
sonatypeUsername = System.getenv("sonatype.username")
sonatypePassword = System.getenv("sonatype.password")

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'org.apache.httpcomponents:httpclient:4.4'

    testCompile 'junit:junit:4.11'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'JaLAPI',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Notronix'
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'JaLAPI'
                packaging 'jar'
                description 'A Java implementation of the Linnworks API'
                url 'https://github.com/Notronix/JaLAPI/'

                scm {
                    url 'scm:git@github.com:Notronix/JaLAPI.git'
                    connection 'scm:git@github.com:Notronix/JaLAPI.git'
                    developerConnection 'scm:git@github.com:Notronix/JaLAPI.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'clint.munden'
                        name 'Clint Munden'
                    }
                }
            }
        }
    }
}